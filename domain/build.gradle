plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.barfuin.gradle.jacocolog' version '2.0.0'
    id 'checkstyle'
    id 'jacoco'
}

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

bootJar {
    manifest {
        attributes 'Start-Class': 'mutant.api.ApiApplication'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
    jacoco {
        excludes = [
//                'mutant.domain.YourClass',
        ]
    }
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
        }
    }
}
jacocoTestReport {
    afterEvaluate { //exclude from report
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
//                'mutant.domain.YourClass',
            ])
        }))
    }
    reports {
        xml.enabled true
        csv.enabled false
    }
}

test.finalizedBy jacocoTestReport
//build.dependsOn jacocoTestCoverageVerification //uncomment this to make it fail

tasks.withType(Checkstyle) {
    reports {
        xml.required = true
        html.required = true
    }
}

checkstyle {
    configFile = project(':').file("config/checkstyle/checkstyle.xml")
    configProperties = ["suppressionFile" : project(':').file("config/checkstyle/suppressions.xml")]
    toolVersion = "6.1.1"
}

task copyAllDependencies(type: Copy) {
    from configurations.compileClasspath
    into 'build/libs/lib'
}
build.dependsOn(copyAllDependencies)